/*
 * $Id: SimpleSample.cdl 1887 2012-09-14 07:26:36Z okuma-top $
 *
 * Simple な Sample
 *
 *  +------------------------------------+           +----------------------+          +-------------+
 *  | mruby object                       |           |                      |          |             |
 *  |bridge=                             | sSample   |  tMrubyBridgesSample |  sSample |   tSample   |
 *  | TMrubyBridgesSampe("SimpleBridge") |==========>|     SimpleBridge     |----------|>  Sample    |
 *  |                                    |  の関数群 |                      |cCall eEnt|             |
 *  |                                    |           |                      |          |             |
 *  +------------------------------------+           +----------------------+          +-------------+
 *
 *・シグニチャ記述
 *
 *   sSample … 関数インタフェースの定義
 *              慣習として接頭文字 s を付加する
 * 
 *    シグニチャ sSampleには 2 つの関数 sayHello と howAreYou がある。
 *    セルタイプ tSample の提供する機能となるので、sSample という名前にした。
 *
 *・セルタイプ記述
 *
 *   tMrubyBridgesSample … ブリッジセルのセルタイプ (MrubyBridgePlugin により生成)
 *   tSample             … 呼び先のセルタイプ
 *
 */

import( "cygwin_kernel.cdl" );

// シグニチャ sSample
signature sSample {
	ER  sayHello( [in]int32_t times );
	ER  howAreYou( [out,string(len)]char_t *buf, [in]int32_t len );
};

// セルタイプ tSample
celltype tSample {
	entry sSample eEnt;
};

[out_through()]   // region 対応が不完全
region rMruby {
// セル Sample
    cell tSample Sample
    {
    };
};

/*
 * シグニチャプラグイン MrubyBridgePlugin の呼び出し。
 *
 * MrubyBridgePlugin により
 * ・ブリッジのセルタイプ tMrubyBridgesSample の定義が 
 *   gen/tmp_MrubyBridgePlugin_0.cdl に生成される。
 *   sSample の部分は、シグニチャ名。
 * ・セルタイプコード gen/tMrubyBridgesSample.c に、mruby の 
 *   TMrubyBridgesSample クラス定義コードが生成される。
 */
generate( MrubyBridgePlugin, sSample, "" );

/*
 * ブリッジセルの設置
 *
 * mruby から、TECS::tsSample クラスの Bridge として参照される。
 *   ex) bridge = TECS::TsSample.new( "SimpleBridge" )
 *
 * cTECS の signature、すなわち sSample の関数が、TMrubyBridgesSample 
 * のインスタンスメソッドとして定義され、呼び出すことができる。
 *   ex) bridge.sayHello( 3 )
 */
region rMruby {
    cell nMruby::tsSample SimpleBridge {
        cTECS = Sample.eEnt;
    };

    /* cell nMruby::tTECSInitializer VM_TECSInitializer; */
    cell nMruby::tMrubyProc VM {
        cInit = VM_TECSInitializer.eInitialize;
    };
};

generate( C2TECSBridgePlugin, nPosix::sMain, "" );

region rMruby {
    cell nC2TECS::tnPosix_sMain Main {
        cCall = VM.eMain;
    };
};
